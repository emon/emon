dnl
dnl  configure.in for notasip
dnl
dnl	$IPhone: configure.in,v 1.2 2000/09/12 10:27:35 cas Exp $
dnl

AC_INIT(rs_emon.c)
AC_CONFIG_HEADER(config.h)

dnl ---------------- Checks for configure options ----------------

AC_ARG_ENABLE(ipv6, [  --disable-ipv6          disable IPv6 support],,
							enable_ipv6=yes)
AC_ARG_ENABLE(ipv4, [  --disable-ipv4          disable IPv4 support],,
							enable_ipv4=yes)

dnl ---------------- Checks for programs ----------------

AC_PROG_MAKE_SET
AC_PROG_CC

dnl ---------------- Checks for libraries ----------------

AC_SEARCH_LIBS(getaddrinfo, inet6)
SDL_VERSION=1.1.6
AM_PATH_SDL_LOCALEXT($SDL_VERSION)

dnl ---------------- Checks for header files ----------------

AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h)
AC_CHECK_HEADERS(sys/soundcard.h, AC_DEFINE(HAVE_SYS_SOUNDCARD_H))
AC_CHECK_HEADERS(machine/soundcard.h, AC_DEFINE(HAVE_MACHINE_SOUNDCARD_H))
AC_CHECK_HEADERS(soundcard.h, AC_DEFINE(HAVE_SOUNDCARD_H))
AC_EGREP_HEADER(sa_len, sys/socket.h, AC_DEFINE(HAVE_SA_LEN))
AC_EGREP_HEADER(sockaddr_storage, sys/socket.h,
				AC_DEFINE(HAVE_SOCKADDR_STORAGE))

AC_MSG_CHECKING([for IPv6 capability])
if test "$enable_ipv6" = yes ; then
	AC_CACHE_VAL(ac_cv_inet6, [
		ac_cv_inet6=no
		AC_EGREP_CPP(AF_INET6_exists, [
#include <sys/socket.h>
#ifdef AF_INET6
	AF_INET6_exists
#endif
		],
		AC_EGREP_CPP(sockaddr_in6, [
#include <sys/types.h>
#include <netinet/in.h>
		], [
			AC_DEFINE(INET6)
			ac_cv_inet6=yes
		]))
	])
	AC_MSG_RESULT($ac_cv_inet6)
else
	AC_MSG_RESULT(skipped)
fi

AC_MSG_CHECKING([for IPv4 capability])
if test "$enable_ipv4" = yes ; then
	AC_CACHE_VAL(ac_cv_inet, [
		ac_cv_inet=no
		AC_EGREP_CPP(AF_INET_exists, [
#include <sys/socket.h>
#ifdef AF_INET
	AF_INET_exists
#endif
		],
		AC_EGREP_CPP(sockaddr_in, [
#include <sys/types.h>
#include <netinet/in.h>
		], [
			AC_DEFINE(INET)
			ac_cv_inet=yes
		]))
	])
	AC_MSG_RESULT($ac_cv_inet)
else
	AC_MSG_RESULT(skipped)
fi

dnl --- Checks for typedefs, structures, and compiler characteristics ---

AC_C_CONST
AC_HEADER_TIME
AC_TYPE_SIZE_T

dnl ---------------- Checks for library functions ----------------

AC_TYPE_SIGNAL
AC_CHECK_FUNCS(gettimeofday select socket)
AC_REPLACE_FUNCS(getaddrinfo getnameinfo net_addrcmp)

dnl ---------------- Checks for features ----------------

if test "$enable_ipv6" = yes -a "$ac_cv_inet6" != yes ; then
	AC_MSG_WARN([this platform has no IPv6 capablity. disabled.])
fi

if test "$enable_ipv4" = yes -a "$ac_cv_inet" != yes ; then
	AC_MSG_WARN([this platform has no IPv4 capablity. disabled.])
fi

if test "$ac_cv_inet6" != yes -a "$ac_cv_inet" != yes ; then
	AC_MSG_ERROR(
[At least, ONE network protocol must be available.
Check your system and --enable-ipv6 and --enable-ipv4 options.])
fi

dnl ----------------------------------------------------------------

AC_OUTPUT(Makefile)

dnl -- EOF --
